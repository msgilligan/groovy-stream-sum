buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

plugins {
    id 'java-library'
    id 'groovy'
}

group = "com.msgilligan.groovystreamsum"
version = "0.1"
description = "A groovy Extension library for stream summing"

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    jcenter()
}

dependencies {
    implementation "org.codehaus.groovy:groovy:${groovyVersion}"
    
    testImplementation 'junit:junit:4.13'
    testImplementation("org.spockframework:spock-core:${spockVersion}") {
            exclude module: "groovy-all"
        }
}

test.dependsOn(jar) // We are testing Groovy extensions and need META-INF/service resource

test {
    useJUnitPlatform()          // We're using Spock 2.0 and JUnit 5
}

// code below this can be safely removed if you don't want publish library ever

apply plugin: 'maven-publish'
apply plugin: 'bintray'

publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {

    user = getPropertyOrUseDefault("bintrayUser", "fake_user")
    key = getPropertyOrUseDefault("bintrayKey", "fake_key")
    publications = ['groovyMaven']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven' // or your repo name
        name = projectName      // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
    }
    dryRun = true // whether to run this as dry-run, without deploying
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}
